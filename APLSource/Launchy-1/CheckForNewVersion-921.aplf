 {r}←CheckForNewVersion dummy;mco;tag;gitVersion;thisVersion;msg;rc;dmx
 r←⍬
 mco←⎕NEW ##.Check4Updates(,⊂'Launchy')
 :Trap 0
     tag←mco.GetLatestTag
 :Else
     TellCommunicationWithGitHubFailed'Launchy'
     :Return
 :EndTrap
 gitVersion←⌊##.GitHubAPIv3.CastTagname2Number tag
 thisVersion←⌊##.GitHubAPIv3.CastTagname2Number 2⊃Version
 :If thisVersion≥gitVersion
     TellLaunchyIsUp2Date ⍬
 :Else
     msg←⊂'There is a better version available on GitHub: ',tag~'v'
     msg,←'' 'Would you like to download this version?'
     :If YesOrNo msg
         :Trap 0
             rc←mco.GetExe tag
         :Else
             dmx←⎕DMX
             TellCommunicationWithGitHubFailed'Launchy'
             :Return
         :EndTrap
         :If rc
             n TellErrorWhileCheckingForBetterVersion''
         :EndIf
     :EndIf
 :EndIf
⍝Done
