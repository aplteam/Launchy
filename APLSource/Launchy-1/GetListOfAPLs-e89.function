 (list orig default)←GetListOfAPLs G;ind
⍝ `list` is a two-item vector with all APLs installed Unicode [1] and Classic [2].
⍝ With each item the list is sorted from 64 down to 32 bit.
⍝ `orig` is sorted in the same way but is a vector of vector. The contents matches
⍝ the items in the "APL versions" combo box and can therefore be used to correct
⍝ the selection.
 list←orig←G.AplVersions
 list←'APL/W '⎕R'APL/W-32 '⊣list
 default←G.AplVersions≡¨⊂G.Default
 list←'[0-9]$'⎕R'& Classic'⊣list
 list←list[ind←⍒(↑list)[;14 15]]
 default←default[ind]
 orig←orig[ind]
 list←list[ind←⍒↑(-⍴'Unicode')↑¨list]
 default←default[ind]
 orig←orig[ind]
 list←↑(⍴'Dyalog APL/W-')↓¨list
 list,←'-'
 list←list[;4 5 6 7 8 16 8 1 2 3 9 10 11 12 13 14 15]
 list←↓((⊃⍴list)↑1↓⎕D,⎕A),'.',' ',' ',list
 :If 0<+/default
     (default/list)←⊂(⊃default/list),' ★'
 :EndIf
⍝Done
