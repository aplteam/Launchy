 r←StartWhichRide caption;∆;n2;startAt;i;version;rf;res
 n2←⎕NS''
 ∆←''
 ∆,←⊂'MaxButton' 0
 ∆,←⊂'MinButton' 0
 ∆,←⊂'SysMenu' 0
 ∆,←⊂'Sizeable' 0
 ∆,←⊂'Posn'((n.Form.Posn+50))
 ∆,←⊂'Size'(300 400)
 ∆,←⊂'Coord' 'Pixel'
 ∆,←⊂'Caption'caption
 n2.Form←⎕NEW'Form'∆

 n2.Label←n2.Form.⎕NEW'Label'(('Caption' 'Start this Ride')('Posn'(10 10)))

 startAt←(10+⊃+⌿↑n2.Label.(Posn Size))10
 i←0
 n2.Options←⍬
 :For version :In G.RideVersions[;1]
     i+←1
     :If version≢'2.0'
         ∆←''
         ∆,←⊂'Caption'('&',version)
         ∆,←⊂'Posn'startAt
         ∆,←⊂'Style' 'Radio'
         rf←n2.Form.⎕NEW'Button'∆
         ⍎'n2.Option',(¯2↑'00',⍕i),'←rf'
         n2.Options,←rf
         startAt[1]+←rf.Size[1]
     :EndIf
 :EndFor
 (¯1↑n2.Options).State←1   ⍝ Default is the best one

 ∆←''
 ∆,←⊂'Caption' 'Yes'
 ∆,←⊂'Default' 1
 ∆,←⊂'Posn'((25+1⊃AddPosnAndSize rf),10)
 ∆,←⊂'Size'(⍬ 80)
 n2.OK←n2.Form.⎕NEW'Button'∆
 n2.OK.Size[1]+←3
 n2.OK.onSelect←1

 ∆←''
 ∆,←⊂'Caption' 'No'
 ∆,←⊂'Cancel' 1
 ∆,←⊂'Posn'(n2.OK.Posn[1],10+2⊃AddPosnAndSize n2.OK)
 ∆,←⊂'Size'(⍬ 80)
 n2.Cancel←n2.Form.⎕NEW'Button'∆
 n2.Cancel.Size[1]+←3
 n2.Cancel.onSelect←1

 n2.Form.Size[1]←10+1⊃AddPosnAndSize n2.OK
 n2.Form.Size[2]←10+(⌈/2⊃¨AddPosnAndSize¨'Label'⎕WN n2.Form)⌈2⊃AddPosnAndSize n2.Cancel

 ⎕NQ n2.OK'GotFocus' ⋄ res←n2.Form.Wait
 :If 0∊⍴res
 :OrIf n2.Cancel≡⊃res
     r←0
 :Else
     r←⊃1+U.Where n2.Options.State
 :EndIf
⍝Done
