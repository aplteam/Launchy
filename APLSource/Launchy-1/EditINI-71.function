 {r}←EditINI n;flags;msg;filename1;filename2;ns;was1;was2;exists
 r←⍬
 n.Form.Active←0
 n.ok.Active←0
 filename1←∆HomeDir,'\launchy.ini'
 ns←⎕NS''
 ns.ini1←was1←U.ReadUtf8File filename1
 filename2←F.PWD,'\Launchy_',S.GetComputerName,'.ini'
 :If exists←F.IsFile filename2
     ns.ini2←was2←U.ReadUtf8File filename2
 :EndIf
 msg←''
 :If exists
     ns.⎕ED↑'ini1' 'ini2'
     :If ∨/flags←(was1≢ns.ini1)(was2≢ns.ini2)
         :If 1⊃flags
             U.WriteUtf8File filename1 ns.ini1
         :EndIf
         :If 2⊃flags
             U.WriteUtf8File filename2 ns.ini2
         :EndIf
         msg←⊂'You have changed ',((+/flags)⊃'one' 'both'),' of Launchy''s INI files.'
     :EndIf
 :Else
     ns.⎕ED'ini1'
     :If was1≢ns.ini1
         U.WriteUtf8File filename1 ns.ini1
         msg←⊂'You have changed Launchy''s INI file.'
     :EndIf
 :EndIf
 :If ~0∊⍴msg
     msg,←'' 'In order to make Launchy honour those' 'changes you need to restart Launchy.'
     ShowMsg msg
 :EndIf
 n.Form.Active←1
 n.ok.Active←1
⍝Done
