 r←Find_RIDE_Versions Ini;allRides;bool;flag;ind;path;versionNos
⍝ Returns a list with all major (like 3.0) and minor (like (3.1) versions of Ride found in the
⍝ path `[Ride]path2ride` is pointing to - if that exists at all.
⍝ This version does not support anymore to install several builts of the same version number.
 r←''
 :If 0<Ini.⎕NC'RIDE'
 :AndIf 2=Ini.RIDE.⎕NC'path2ride'
 :AndIf F.IsDir Ini.RIDE.path2ride
     flag←'programs\dyalog'≡U.Lowercase{⍵↑⍨-+/2>+\(⌽⍵)∊'/\'}Ini.RIDE.path2ride
     path←Ini.RIDE.path2ride,flag/'\..'
 :AndIf ~0∊⍴allRides←⌽⊃('recursive' 1)F.Dir path,'\*.*.exe'
     allRides←(-1+⍴¨'\'U.Last¨allRides)↓¨allRides
     versionNos←'\'U.Last¨allRides
     bool←'Ride-'∘{⍺≢(⍴⍺)↑⍵}¨versionNos
     (bool/versionNos)←{⍵/⍨2>+\'.'=⍵}¨bool/versionNos
     ind←∪#{⍵⍳⍵}versionNos
     r←⊖(versionNos,[1.5]allRides)[ind;]
 :EndIf
