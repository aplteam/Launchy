 Ini←ProcessIniFile errParms;iniFilename;iniFilename2;msg;rc;more;parms;qdmx;EndIf;q
⍝ Process the main INI file (that MUST exist) and then check whether there is also
⍝ an INI file for the current machine available. If that is the case, this one
⍝ is processed as well. That way one cn overwrite general values with machine
⍝ dependent ones.
 Ini←⍬
 errParms.addToErrMsg←' "ProcessIniFile"'
 iniFilename←∆HomeDir,'\Launchy.ini'
 iniFilename2←∆HomeDir,'\Launchy_',S.GetComputerName,'.ini'
 :If ∧/⎕NEXISTS¨iniFilename iniFilename2
     :Trap 0
         Ini←⎕NEW ##.IniFiles(,⊂iniFilename iniFilename2)
     :Else
         qdmx←⎕DMX
         :Trap 0
             Ini←⎕NEW ##.IniFiles(,⊂iniFilename)
             q←'The INI file:'('   ',iniFilename2)'has a problem, therefore Launchy cannot start.' 'Location is:'
             q,←('   ',iniFilename2)('(',qdmx.EM,')')''('Edit with Notepad?')
             :If YesOrNo q
                 ⎕CMD'notepad ',iniFilename2
             :EndIf
             →
         :Else
             q←'The INI file:'('   ',iniFilename)'has a problem, therefore Launchy cannot start.' 'Location is:'
             q,←('   ',iniFilename)('(',qdmx.EM,')')''('Edit with Notepad?')
             :If YesOrNo q
                 ⎕CMD'notepad ',iniFilename
             :EndIf
             →
         :EndTrap
     :EndTrap
 :ElseIf ⎕NEXISTS iniFilename
     Ini←⎕NEW ##.IniFiles(,⊂iniFilename)
 :Else
     :If ⎕NEXISTS iniFilename,'.removeme'
         msg←'Cannot find the requiered INI file' ''
         msg,←'However, there is a file:'(iniFilename,'.removeme')''
         msg,←'Would you like me to rename this to 'iniFilename'and then process it?'
         :If YesOrNo msg
             (rc more)←(iniFilename,'.removeme')F.MoveTo iniFilename
             :If 0=rc
                 →1
             :Else
                 msg←⊂'Something went wrong renaming the INI file!'
                 msg,←''('(RC=',(⍕rc),{0=≢⍵:')' ⋄ '; ',⍵,')'}more)
                 ShowMsg2 msg
                 Off
             :EndIf
         :Else
             ShowMsg2'Cannot find the requiered INI file'
             Off
         :EndIf
     :Else
         ShowMsg2'Cannot find the requiered INI file'
         Off
     :EndIf
 :EndIf
 Ini←PolishPath_RIDE Ini
 Ini←Ini.Convert ⎕NS''
 CheckIni Ini
 errParms.⎕EX'addToErrMsg'
 :If 0=Ini.⎕NC'EXEC_SETUP'
     'EXEC_SETUP'Ini.⎕NS''
     Ini.EXEC_SETUP.default←1   ⍝ Establish default
 :EndIf
⍝Done
