 {r}←{workspace}LaunchDyalog(ty ws dseType port);p;parms;rc;tempfile;dir;kai;msg;en;success;flag
⍝ Compiles the "batch script" and finally launches any version of Dyalog.
 r←1
 workspace←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'workspace'
 p←GetInstallPath ty
 :If 0=≢p
     r←TellRegistryProblem ty
 :Else
     parms←##.OS.CreateParms_WinExecute
     parms.file←p,'Dyalog.exe'
     (parms.lpParms flag)←n CollectCommandLineParms port
     :If 0≠port
         parms.lpParms←('RIDE_INIT=SERVE::',(⍕port)),' ',parms.lpParms   ⍝ Make it listen to ride
     :EndIf
     parms.lpParms,←{0=≢⍵:⍵ ⋄ ⊂' ',⍵}workspace                           ⍝ ⍺ might be a workspace name
     parms.lpDirectory←ExecuteIn ¯1↓⊃⎕NPARTS parms.file
     parms.lpDirectory{F.IsDir ⍺:⍺ ⋄ ⍵}←''                               ⍝ Flatten in case the directory does not exist
     parms.show←0≡port
     n.showCmd.Checked←AskForShowCommandLineParms⍣(flag∧∆AskForCommandLineParms∧0=n.showCmd.Checked)⊣n.showCmd.Checked
     :If 0≠n.showCmd.Checked ShowCmd parms n.runAsAdmin.State
         parms.lpParms←⊃,/' ',¨parms.lpParms
         (success rc msg)←n.runAsAdmin.State ##.OS.WinExecute parms
         :If 1≠success
             ShowMsg2'Error! Could not start:' ''parms.file('in ',parms.lpDirectory)(S.IsRunningAsAdmin/'with admin rights')
         :EndIf
     :Else
         r←0
     :EndIf
 :EndIf
⍝Done
