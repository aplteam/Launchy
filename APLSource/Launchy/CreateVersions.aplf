 n←CreateVersions n;∆;msg;whichVersion;okFlag;orig;aplList;indents
⍝
 (aplList orig)←GetListOfAPLs G
 (aplList indents orig)←MassageAplList aplList orig
 whichVersion←orig≡¨⊂G.Default
 :If okFlag←1=+/whichVersion                ⍝ Exactly one default - fine!
     G.LastSelectedVersion←(whichVersion⍳1)⊃orig
 :Else
     :If IniHas_DyalogDefault Ini
     :AndIf (⊂Ini.DYALOG.default)∊G.AplVersions
     ⍝ We do this only once to support the user with old INI files.
         G.Default←Ini.DYALOG.default
         whichVersion←G.AplVersions≡¨⊂G.Default
         okFlag←1
     :Else
         G.Default←⊃G.AplVersions               ⍝ Make the first one the default, at least for the time being
         whichVersion←(⍴G.AplVersions)↑1
     :EndIf
     G.LastSelectedVersion←G.Default
 :EndIf

 ∆←''
 ∆,←⊂'Caption'(' ',(⍕⍴G.AplVersions),' APL Version',((1<⍴G.AplVersions)/'s'),' installed ')
 ∆,←⊂'Posn'(5 2)
 ∆,←⊂'Size'(50,n.GeneralSubForm.Size[2]-5)
 n.AplVersionGroup←n.GeneralSubForm.⎕NEW'Group'∆

 ∆←''
 ∆,←⊂'Posn'(17 10)
 ∆,←⊂'Size'(⍬(n.AplVersionGroup.Size[2]-2×10))
 ∆,←⊂'Items'aplList
 ∆,←⊂'indents'indents
 ∆,←⊂'Style' 'Drop'
 ∆,←⊂'Rows'(2+≢aplList)
 ∆,←⊂'TipObj'n.tip
 ∆,←⊂'Tip' 'The default version is marked with a ★'
 ∆,←⊂'SelItems'whichVersion
 n.List←n.AplVersionGroup.⎕NEW'ComboEx'∆
 n.List.onKeyPress←'OnKeyPressInList'
 n.List.onSelect←'OnSelectList'
 n.List.∆Orig←orig
 n.List.∆DefaultSelection←whichVersion

 :If 0=okFlag
 :AndIf ~0∊≢G.AplVersions
     msg←''
     msg,←⊂'There was no default version of Dyalog defined yet.'
     msg,←⊂'This version was selected as default for you:'
     msg,←⊂''
     msg,←⊂'   ',G.Default
     msg,←⊂''
     msg,←⊂'You can change this via the "Options" menu (F4).'
     ShowMsg2 msg
 :EndIf
⍝Done
