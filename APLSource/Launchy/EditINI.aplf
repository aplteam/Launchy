 {r}←EditINI n;flags;msg;filename1;filename2;ns;was1;was2;exists
 r←⍬
 n.Form.Active←0
 n.ok.Active←0
 filename1←∆HomeDir,'\launchy.ini'
 ns←⎕NS''
 ns.GeneralIni←was1←⊃F.NGET filename1
 filename2←∆HomeDir,'\Launchy_',S.GetComputerName,'.ini'
 :If exists←F.IsFile filename2
     ns.ComputerSpecificIni←was2←⊃F.NGET filename2
 :EndIf
 msg←''
 :If exists
     ns.⎕ED↑'GeneralIni' 'ComputerSpecificIni'
     :If ∨/flags←(was1≢ns.GeneralIni)(was2≢ns.ComputerSpecificIni)
         :If 1⊃flags
             (⊂ns.GeneralIni)F.NPUT filename1 1
         :EndIf
         :If 2⊃flags
             (⊂ns.ComputerSpecificIni)F.NPUT filename2 1
         :EndIf
         msg←⊂'You have changed ',((+/flags)⊃'one' 'both'),' of Launchy''s INI files.'
     :EndIf
 :Else
     ns.⎕ED'GeneralIni'
     :If was1≢ns.GeneralIni
         (⊂ns.GeneralIni)F.NPUT filename1 1
         msg←⊂'You have changed Launchy''s INI file.'
     :EndIf
 :EndIf
 :If ~0∊⍴msg
     msg,←'' 'In order to make Launchy honour changes restart Launchy.'
     ShowMsg2 msg
 :EndIf
 n.Form.Active←1
 n.ok.Active←1
⍝Done
