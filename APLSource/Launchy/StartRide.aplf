 {r}←{cmd}StartRide pathToExe;parms;rc;bat;success;msg
 r←0
 cmd←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'cmd'
 parms←##.OS.CreateParms_WinExecute
 bat←''
 :If 2=Ini.RIDE.⎕NC'profile'
 :AndIf 0≠≢Ini.RIDE.profile
     bat,←'SET RIDE_PREFS=',Ini.RIDE.profile,'&'
 :EndIf
 :If 0≠≢cmd
     bat,←' ',cmd,'&'
 :EndIf
 parms.file←'bat'F.GetTempFilename2''
 parms.show←0
 bat,←' "',('"'~⍨GetRideExeName pathToExe),'"'
 ##.APLTreeUtils.WriteUtf8File parms.file bat
 (success rc msg)←##.OS.WinExecute parms
 :If 0=success
     'Could not start Ride'CheckRc rc
 :EndIf
 {⎕DL 1 ⋄ F.DeleteFile ⍵}&parms.file
⍝Done
