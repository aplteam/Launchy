 (list orig)←GetListOfAPLs G;ind;default
⍝ `list` is a two-item vector with all APLs installed Unicode [1] and Classic [2].
⍝ With each item the list is sorted from 64 down to 32 bit.
⍝ `orig` is sorted in the same way but is a vector of vectors.
 list←orig←G.AplVersions
 list←'APL/W '⎕R'APL/W-32 '⊣list
 default←G.AplVersions≡¨⊂G.Default
 :If 0≠≢list←'[0-9]$'⎕R'& Classic'⊣list
     list←list[ind←⍒(↑list)[;14 15]]
     default←default[ind]
     orig←orig[ind]
     list←list[ind←⍒↑(-⍴'Unicode')↑¨list]
     default←default[ind]
     orig←orig[ind]
     :If 0<+/default
         (default/list)←⊂(⊃default/list),' ★'
     :EndIf
 :EndIf
⍝Done
