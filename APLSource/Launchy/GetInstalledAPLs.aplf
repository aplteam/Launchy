 list←GetInstalledAPLs dummy;msg
⍝ Examines the Registry in order to find out which versions of
⍝ Dyalog are installed on the current machine
 :If 0∊≢list←R.GetAllSubKeyNames'Software\Dyalog'
 :OrIf 0∊≢list←(50<⊃∘⍴¨R.GetAllValueNames¨(⊂'Software\Dyalog\'),¨list)/list
     msg←⊂'No version of Dyalog was found'
     msg,←⊂''
     msg,←⊂'In case you have just installed Dyalog'
     msg,←⊂'you might need to reboot your machine now.'
     'Launchy'ShowMsg2 msg
 :Else
     list←{⍵⌿⍨∨/'Dyalog APL/W'⍷↑⍵}list
     list←2⊃¨'\'U.SplitPath¨list
     list←(0<⊃∘⍴¨list~¨' ')/list
     list←list[⍒{⍎⊃{⍵/⍨⊃∘⊃∘⎕VFI¨⍵}⍵⊂⍨' '=⍵}¨list]
 :EndIf
